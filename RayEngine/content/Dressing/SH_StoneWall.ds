
#include "Shaders/SH_Default_ds.si"

void main()
{
    // Check deferred ID
    vec4 deferredData = texture(TexDeferredData, TexCoord);
    if (CheckID(deferredData.r, DeferredID))
        discard;
    
    // Sample textures
    vec3 worldPosition = texture(TexPosition, TexCoord.xy).rgb;
    vec3 worldNormal = texture(TexNormal, TexCoord.xy).xyz;
    vec4 color = texture(TexColor, TexCoord.xy).rgba;

    float noiseScale = 0.5;
    float yStepNoiseScale = 0.1;
    float yStepScale = 1.2;
    float yTileScale = 1.3;
    float noisePrecision = 4.0;
    float bigNoiseScale = 0.1;

    float noise = abs(SimplexNoise3D(worldPosition * noiseScale));
    noise = pow(noise, 0.3);
    float yStep = worldPosition.y + SimplexNoise3D(worldPosition * yStepNoiseScale) * yStepScale;
    noise *= abs(cos(yStep * yTileScale));
    float bigNoise = SimplexNoise3D(worldPosition * bigNoiseScale);
    bigNoise = pow(bigNoise, 0.3);
    noise *= max(0.2, bigNoise);
    color.rgb = max(vec3(0.1), vec3(noise));

    // Camera distance and dot
    vec3 cameraDifference = CameraPosition - worldPosition;
    float cameraDistance = length(cameraDifference);
    vec3 cameraDirection = cameraDifference / cameraDistance;
    float cameraDot = dot(cameraDirection, worldNormal);

    // Fade
    float cameraDistanceFade = DistanceFade(cameraDistance);
    float cameraFade = mix(cameraDistanceFade, cameraDot * cameraDistanceFade, FADE_DOT_PART);
    float aoFade = pow(texture(TexAO, TexCoord).x, 0.5);
    color.rgb *= max(0.2, 1.0 - aoFade);

    Output = vec4(color.rgb * vec3(cameraFade), color.a); 
}
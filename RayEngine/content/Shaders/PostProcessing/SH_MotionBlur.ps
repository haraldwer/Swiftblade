#include "Shaders/Uniforms/SH_FrameUniforms.si"
#include "Shaders/Uniforms/SH_TexUniforms.si"
#include "Shaders/Utility/SH_Utility.si"

in vec2 TexCoord;
layout (location = 0) out vec4 Output;

const int SAMPLES = 16;
const float THRESHOLD = 0.001f;
const float EDGE_FADE = 0.15f;
const float DEPTH_THRESHOLD = 1.0f;

void main()
{
    Output = texture(TexFrame, TexCoord);
    
    vec3 vel = texture(TexData, TexCoord).rgb;
    if (length(vel) < THRESHOLD)
        return;
    
    float maxEdge = max(max(max(TexCoord.x, 1.0f - TexCoord.x), TexCoord.y), 1.0f - TexCoord.y);
    float edgePart = 1.0f - EDGE_FADE;
    float edge = 1.0f - clamp((maxEdge - edgePart) / (1.0f - edgePart), 0.0f, 1.0f);
    
    vec3 part = vel / float(SAMPLES);
    vec3 result = Output.rgb;
    for (int i = 1; i < SAMPLES; i++)
    {
        vec2 offset = -part.xy * (float(i) - float(SAMPLES) * 0.5f) * edge;
        vec2 coord = clamp(TexCoord + offset, 0.0f, 1.0f);
        result += texture(TexFrame, coord).rgb;
    }
    Output.rgb = result / float(SAMPLES);
    Output.a = 1.0f;
}
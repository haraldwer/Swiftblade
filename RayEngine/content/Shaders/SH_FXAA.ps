#version 330

#include "Shaders/SH_FrameUniforms.si"

uniform sampler2D TexQuantize;

in vec2 TexCoord;
out vec4 Output;


// Tutorial:
// https://catlikecoding.com/unity/tutorials/advanced-rendering/fxaa/

// Trims the algorithm from processing darks.
//   0.0833 - upper limit (default, the start of visible unfiltered edges)
//   0.0625 - high quality (faster)
//   0.0312 - visible limit (slower)
const float contrastThreshold = 0.0625;

// The minimum amount of local contrast required to apply algorithm.
//   0.333 - too little (faster)
//   0.250 - low quality
//   0.166 - default
//   0.125 - high quality 
//   0.063 - overkill (slower)
const float relativeThreshold = 0.166f;

// Choose the amount of sub-pixel aliasing removal.
// This can effect sharpness.
//   1.00 - upper limit (softer)
//   0.75 - default amount of filtering
//   0.50 - lower limit (sharper, less sub-pixel aliasing removal)
//   0.25 - almost off
//   0.00 - completely off
const float subpixelBlending = 0.75f;

// Maximum numbers of steps when getting edge blend
const int edgeStepCount = 10; // Low: 4;
// Step size for each step in the blending process
const float edgeSteps[10] = { 1.0f, 1.5f, 2.0f, 2.0f, 2.0f, 2.0f, 2.0f, 2.0f, 2.0f, 4.0f }; // Low: { 1.0f, 1.5f, 2.0f, 4.0f };
// Used if couldn't find edge blend when stepping
const float edgeGuess = 8.0f; // Low: 12.0f;

struct LuminanceData {
    float m, n, e, s, w;
    float ne, nw, se, sw;
    float highest, lowest, contrast;
};

vec2 GetTexelSize()
{
    return vec2(1.0f / Resolution.x, 1.0f / Resolution.y);
}

float LinearRGBToLuminance(vec3 linearRgb)
{
    return dot(linearRgb, vec3(0.2126729f,  0.7151522f, 0.0721750f));
}

vec4 Sample(vec2 uv)
{
    uv = vec2(uv.x, 1.0 - uv.y);
    return texture(TexQuantize, uv);
}

float SampleLuminance(vec2 uv, float uOffset, float vOffset)
{
    uv += GetTexelSize() * vec2(uOffset, vOffset);
    vec4 color = Sample(uv);
    // Might want to convert to linear, currently using gamma
    return LinearRGBToLuminance(clamp(color.rgb, 0.0, 1.0));
}

LuminanceData SampleLuminanceNeighborhood (vec2 uv) {
    LuminanceData l;
    l.m = SampleLuminance(uv, 0,  0);
    l.n = SampleLuminance(uv, 0,  1);
    l.e = SampleLuminance(uv, 1,  0);
    l.s = SampleLuminance(uv, 0, -1);
    l.w = SampleLuminance(uv,-1,  0);

    l.ne = SampleLuminance(uv,  1,  1);
    l.nw = SampleLuminance(uv, -1,  1);
    l.se = SampleLuminance(uv,  1, -1);
    l.sw = SampleLuminance(uv, -1, -1);

    l.highest = max(max(max(max(max(max(max(max(l.sw, l.se), l.nw), l.ne), l.n), l.e), l.s), l.w), l.m);
    l.lowest = min(min(min(min(min(min(min(min(l.sw, l.se), l.nw), l.ne), l.n), l.e), l.s), l.w), l.m);
    l.contrast = l.highest - l.lowest;
    return l;
}

bool ShouldSkipPixel (LuminanceData l)
{
    float threshold = max(contrastThreshold, relativeThreshold * l.highest);
    return l.contrast < threshold;
}

float DeterminePixelBlendFactor (LuminanceData l)
{
    float f = 2 * (l.n + l.e + l.s + l.w);
    f += l.ne + l.nw + l.se + l.sw;
    f *= 1.0 / 12;
    f = abs(f - l.m);
    f = clamp(f / l.contrast, 0.0, 1.0);
    float blendFactor = smoothstep(0, 1, f);
    return blendFactor * blendFactor * subpixelBlending;
}

struct EdgeData
{
    bool isHorizontal;
    float pixelStep;
    float oppositeLuminance, gradient;
};

EdgeData DetermineEdge(LuminanceData l)
{
    EdgeData e;
    float horizontal =
    abs(l.n + l.s - 2 * l.m) * 2 +
    abs(l.ne + l.se - 2 * l.e) +
    abs(l.nw + l.sw - 2 * l.w);
    float vertical =
    abs(l.e + l.w - 2 * l.m) * 2 +
    abs(l.ne + l.nw - 2 * l.n) +
    abs(l.se + l.sw - 2 * l.s);
    e.isHorizontal = horizontal >= vertical;

    float pLuminance = e.isHorizontal ? l.n : l.e;
    float nLuminance = e.isHorizontal ? l.s : l.w;
    float pGradient = abs(pLuminance - l.m);
    float nGradient = abs(nLuminance - l.m);

    vec2 texSize = GetTexelSize();
    e.pixelStep = e.isHorizontal ? texSize.y : texSize.x;

    if (pGradient < nGradient)
    {
        e.pixelStep = -e.pixelStep;
        e.oppositeLuminance = nLuminance;
        e.gradient = nGradient;
    }
    else
    {
        e.oppositeLuminance = pLuminance;
        e.gradient = pGradient;
    }

    return e;
}

float DetermineEdgeBlendFactor(LuminanceData l, EdgeData e, vec2 uv) {
    vec2 uvEdge = uv;
    vec2 edgeStep;
    vec2 texSize = GetTexelSize();
    if (e.isHorizontal)
    {
        uvEdge.y += e.pixelStep * 0.5;
        edgeStep = vec2(texSize.x, 0);
    }
    else
    {
        uvEdge.x += e.pixelStep * 0.5;
        edgeStep = vec2(0, texSize.y);
    }

    float edgeLuminance = (l.m + e.oppositeLuminance) * 0.5;
    float gradientThreshold = e.gradient * 0.25;

    // Sample positive
    vec2 puv = uvEdge + edgeStep * edgeSteps[0];
    float pLuminanceDelta = SampleLuminance(puv, 0, 0) - edgeLuminance;
    bool pAtEnd = abs(pLuminanceDelta) >= gradientThreshold;

    for (int i = 1; i < edgeStepCount && !pAtEnd; i++)
    {
        puv += edgeStep * edgeSteps[i];
        pLuminanceDelta = SampleLuminance(puv, 0, 0) - edgeLuminance;
        pAtEnd = abs(pLuminanceDelta) >= gradientThreshold;
    }
    if (!pAtEnd) {
        puv += edgeStep * edgeGuess;
    }

    // Sample negative
    vec2 nuv = uvEdge - edgeStep * edgeSteps[0];
    float nLuminanceDelta = SampleLuminance(nuv, 0, 0) - edgeLuminance;
    bool nAtEnd = abs(nLuminanceDelta) >= gradientThreshold;

    for (int i = 1; i < edgeStepCount && !nAtEnd; i++)
    {
        nuv -= edgeStep * edgeSteps[i];
        nLuminanceDelta = SampleLuminance(nuv, 0, 0) - edgeLuminance;
        nAtEnd = abs(nLuminanceDelta) >= gradientThreshold;
    }
    if (!nAtEnd) {
        nuv -= edgeStep * edgeGuess;
    }

    float pDistance, nDistance;
    if (e.isHorizontal)
    {
        pDistance = puv.x - uv.x;
        nDistance = uv.x - nuv.x;
    }
    else
    {
        pDistance = puv.y - uv.y;
        nDistance = uv.y - nuv.y;
    }

    float shortestDistance;
    bool deltaSign;
    if (pDistance <= nDistance)
    {
        shortestDistance = pDistance;
        deltaSign = pLuminanceDelta >= 0;
    }
    else
    {
        shortestDistance = nDistance;
        deltaSign = nLuminanceDelta >= 0;
    }

    if (deltaSign == (l.m - edgeLuminance >= 0))
    {
        return 0;
    }
    return 0.5 - shortestDistance / (pDistance + nDistance);
}

vec4 ApplyFXAA()
{
    vec2 uv = vec2(TexCoord.x, 1.0 - TexCoord.y);
    LuminanceData l = SampleLuminanceNeighborhood(uv);
    if (ShouldSkipPixel(l))
    {
        return Sample(uv);
    }
    
    float pixelBlend = DeterminePixelBlendFactor(l);
    EdgeData e = DetermineEdge(l);
    float edgeBlend = DetermineEdgeBlendFactor(l, e, uv);
    float finalBlend = max(pixelBlend, edgeBlend);
    
    if(e.isHorizontal)
    {
        uv.y += e.pixelStep * finalBlend;
    }
    else
    {
        uv.x += e.pixelStep * finalBlend;
    }
    return vec4(Sample(uv).rgb, 1.0);
}

void main()
{
    vec4 fxaa = ApplyFXAA();
    Output.rgb = fxaa.rgb;
    Output.a = 1.0f;
}
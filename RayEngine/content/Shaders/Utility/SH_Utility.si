
#include "Shaders/Uniforms/SH_FrameUniforms.si"

const float PI = 3.1415926535f;

vec2 WorldPosToCoord(vec3 InWorldPosition, mat4 InMVP)
{    
    vec4 screenPos = InMVP * vec4(InWorldPosition, 1.0);
    vec2 coord = (1 + screenPos.xy / screenPos.w) / 2;
    return coord;
}

vec2 GetPrevCoord(vec3 InWorldPosition)
{
    return WorldPosToCoord(InWorldPosition, ViewProjPrev);
}

float SquaredLength(vec2 InDist)
{
    return InDist.x * InDist.x + InDist.y * InDist.y;
}

float SquaredLength(vec3 InDist)
{
    return InDist.x * InDist.x + InDist.y * InDist.y + InDist.z * InDist.z;
}

float SquaredLength(vec4 InDist)
{
    return InDist.x * InDist.x + InDist.y * InDist.y + InDist.z * InDist.z + InDist.w * InDist.w;
}

float Pow2(float InValue)
{
    return InValue * InValue;
}

vec4 WorldPosToTex(vec4 InPos)
{
    return vec4(InPos.xyz - CameraPosition, InPos.w);
}

vec3 GetViewDir(vec2 InUV)
{
    vec2 coord = InUV * 2 - 1;
    vec3 off = vec3(coord, 1.0f);
    vec4 dir = ViewProjInv * vec4(off, 1.0f);
    return normalize(dir.xyz);
}

// Spheres and octahedrons
// https://gamedev.stackexchange.com/questions/169508/octahedral-impostors-octahedral-mapping

vec3 UVtoOctahedronDir(vec2 InUV)
{
    // Unpack the 0...1 range to the -1...1 unit square.
    vec2 uv = InUV * 2.0f - 1.0f;
    vec3 position = vec3(uv.x, 0.0f, uv.y);

    // "Lift" the middle of the square to +1 z, and let it fall off linearly
    // to z = 0 along the Manhattan metric diamond (absolute.x + absolute.y == 1),
    // and to z = -1 at the corners where position.x and .y are both = +-1.
    vec2 absolute = abs(position.xy);
    position.y = 1.0f - absolute.x - absolute.y;

    // "Tuck in" the corners by reflecting the xy position along the line y = 1 - x
    // (in quadrant 1), and its mirrored image in the other quadrants.
    if(position.y < 0)
    {
        position.xz = sign(position.xz) * vec2(1.0f - absolute.y, 1.0f - absolute.x);
    }

    return position;
}

vec2 OctahedronDirToUV(vec3 InDir)
{
    vec3 octant = sign(InDir);

    // Scale the vector so |x| + |y| + |z| = 1 (surface of octahedron).
    float sum = dot(InDir, octant);
    vec3 octahedron = InDir / sum;

    // "Untuck" the corners using the same reflection across the diagonal as before.
    // (A reflection is its own inverse transformation).
    if(octahedron.y < 0) 
    {
        vec3 absolute = abs(octahedron);
        octahedron.xz = octant.xz * vec2(1.0f - absolute.z, 1.0f - absolute.x);
    }

    return octahedron.xy * 0.5f + 0.5f;
}

vec4 SampleOct(sampler2D InSampler, vec3 InDir)
{
    return texture(InSampler, OctahedronDirToUV(InDir));
}


// Atlas cube sampling

vec3 GetFlatCubeUV(vec3 InDir)
{
    //Vector Parallel to the unit vector that lies on one of the cube faces
    float a = max(abs(InDir.x), max(abs(InDir.y), abs(InDir.z)));
    float xa = InDir.x / a;
    float ya = InDir.y / a;
    float za = InDir.z / a;
    
    vec2 uv;
    float side = 0;
    
    if (xa > 0.999)
    {
        // Right, index 0, rotate left
        side = 0;
        uv = vec2(-za, -ya);
        uv = vec2(-za, -ya);
    }
    
    if (xa < -0.999)
    {
        // Left, index 1, rotate right
        side = 1;
        uv = vec2(za, ya);
    }
    
    if (ya > 0.999)
    {
        // Up, index 2, upside down and flipped
        side = 2;
        uv = vec2(-xa, -za);
    }
    
    if (ya < -0.999)
    {
        // Down, index3, correct
        side = 3;
        uv = vec2(xa, za);
    }
    
    if (za > 0.999)
    {
        // Forwards, index4, correct
        side = 4;
        uv = vec2(xa, ya);
    }
    
    if (za < -0.999)
    {
        // Backwards, index4, correct
        side = 5;
        uv = vec2(xa, ya);
    }
    
    uv = (uv - 1) * 0.5;
    
    if (uv.x < 0)
        uv.x += 1;
    if (uv.x > 1)
        uv.x -= 1;

    if (uv.y < 0)
        uv.y += 1;
    if (uv.y > 1)
        uv.y -= 1;
    
    return vec3(uv, side);
}

vec4 SampleCubeAtlas(sampler2D InAtlas, vec4 InRect, vec3 InDir)
{
    vec3 uv = GetFlatCubeUV(InDir);
    vec2 rectCoord = InRect.xy + InRect.zw * (uv.xy + vec2(uv.z, 0));
    vec2 rectUV = rectCoord / Resolution;
    return texture(InAtlas, rectUV);
}
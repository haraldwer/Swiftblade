
#include "Shaders/Uniforms/SH_FrameUniforms.si"
#include "Shaders/PBR/SH_PBR_Common.si"

// From LearnOpenGL
// https://learnopengl.com/PBR/Lighting

const float MAX_REFLECTION_LOD = 4.0;

vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 FresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

float AttenuationFunction(float InDistance, float InRange)
{
    return Pow2(Pow2(1 - InDistance / InRange)); // Realistic attenuation: 1 / Pow2(distance);
}

// PBR equation for per-light radiance
vec3 PBRLight(vec3 InWorldPos, vec3 InNormal, vec3 InAlbedo, vec3 InSurface, vec3 InLightPos, vec3 InLightColor, float InLightRange)
{
    float metallic = InSurface.r;
    float roughness = InSurface.g;
    float ao = InSurface.b;

    vec3 N = normalize(InNormal);
    vec3 V = normalize(CameraPosition - InWorldPos);

    vec3 F0 = vec3(0.04);
    F0 = mix(F0, InAlbedo, metallic);

    // Calculate per-light radiance
    vec3 diff = InLightPos - InWorldPos;
    float distance = length(diff);
    vec3 L = diff / distance;
    vec3 H = normalize(V + L);
    float attenuation = AttenuationFunction(distance, InLightRange);
    vec3 radiance = InLightColor * attenuation;

    // cook-torrance brdf
    float NDF = DistributionGGX(N, H, roughness);
    float G = GeometrySmith(N, V, L, roughness);

    // Calculate fresnel
    vec3 F = FresnelSchlickRoughness(max(dot(H, V), 0), F0, roughness);
    vec3 kD = vec3(1) - F; // kS = F
    kD *= 1 - metallic;

    vec3 numerator = NDF * G * F;
    float denominator = 4 * max(dot(N, V), 0) * max(dot(N, L), 0) + 0.0001;
    vec3 specular = numerator / denominator;

    // Add to outgoing radiance Lo
    float NdotL = max(dot(N, L), 0);
    return (kD * InAlbedo / PI + specular) * radiance * NdotL;
}

#include "Shaders/Uniforms/SH_FrameUniforms.si"
#include "Shaders/PBR/SH_PBR_Common.si"
#include "Shaders/Lumin/SH_LuminSample.si"

// From LearnOpenGL
// https://learnopengl.com/PBR/Lighting

vec3 PBR(vec3 InWorldPos, vec3 InNormal, vec3 InAlbedo, vec3 InSurface)
{
    float metallic = InSurface.r;
    float roughness = InSurface.g;
    float ao = InSurface.b;
    
    vec3 N = InNormal;
    vec3 V = Normalize(CameraPosition - InWorldPos);
    vec3 R = reflect(-V, N);
    
    vec3 F0 = vec3(0.04f);
    F0 = mix(F0, InAlbedo, metallic);
    
    // Calculate fresnel from view vector
    vec3 F = FresnelSchlickRoughness(max(dot(N, V), 0.0f), F0, roughness);
    vec3 kD = vec3(1.0f) - F; // kS = F
    kD *= 1.0f - metallic;
    
    // Irradiance -> Diffuse
    vec3 irradiance = SampleIrradiance(InWorldPos, InNormal).rgb;
    vec3 diffuse    = irradiance * InAlbedo;

    // Radiance + BRDF -> Specular
    vec3 radiance = SampleRadiance(probe, InNormal, R, roughness).rgb;
    vec2 envBRDF  = SampleBRDF(N, V, roughness);
    vec3 specular = radiance * (F * envBRDF.x + envBRDF.y);

    // Add diffuse and specular
    // TODO: Break into lower res
    return (kD * diffuse + specular) * ao;
}

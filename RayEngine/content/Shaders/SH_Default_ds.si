
#include "Shaders/Uniforms/SH_FrameUniforms.si"
#include "Shaders/Uniforms/SH_TexUniforms.si"

#include "Shaders/Utility/SH_Utility.si"
#include "Shaders/Utility/SH_DeferredID.si"

#include "Shaders/PBR/SH_PBR.si"

in vec2 TexCoord;
in vec2 RectCoord;
out vec4 Output;

const float DIST_FADE_START = 20.0;
const float DIST_FADE_END = 50.0;
const float FADE_DOT_PART = 0.5; 

float DistanceFade(float InCameraDistance)
{
    return 1.0f - Saturate((InCameraDistance - DIST_FADE_START) / (DIST_FADE_END - DIST_FADE_START));
}

float SurfaceFade(vec3 InWorldPos, vec3 InNormal)
{
    // Camera distance and dot
    vec3 cameraDifference = CameraPosition - InWorldPos;
    float cameraDistance = length(cameraDifference);
    vec3 cameraDirection = cameraDifference / cameraDistance;
    float cameraDot = dot(cameraDirection, InNormal);

    // Fade
    float cameraDistanceFade = DistanceFade(cameraDistance);
    return mix(cameraDistanceFade, cameraDot * cameraDistanceFade, FADE_DOT_PART);
}

void Default(vec3 InAlbedo, vec3 InSurface)
{
    if (CheckID(TexCoord, DeferredID))
        discard;

    vec3 worldPosition = SampleWorldPos(TexCoord).xyz;
    vec3 worldNormal = SampleNormal(TexCoord);
    
    vec3 pbr = PBR(worldPosition, worldNormal, InAlbedo, InSurface);
    Output = vec4(pbr, 1.0);
}

void DefaultMatte(vec3 InAlbedo)
{
    Default(InAlbedo, vec3(0, 1, 1));
}

void DefaultMetal(vec3 InAlbedo)
{
    Default(InAlbedo, vec3(1, 1, 1));
}

void DefaultShiny(vec3 InAlbedo)
{
    Default(InAlbedo, vec3(0, 0, 1));
}